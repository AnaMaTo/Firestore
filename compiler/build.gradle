/*
 * Copyright (c) 2018 Otalia Studios. Author: Mattia Iavarone.
 */

apply plugin: 'kotlin'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    libVersion = rootProject.ext.libVersion
    libGroup = rootProject.ext.libGroup
    libName = 'Firestore'
    libDescription = 'The efficient wrapper for Firestore model data.'
    libArtifactId = 'firestore-compiler'
    githubUrl = rootProject.ext.githubUrl
    githubGit = rootProject.ext.githubGit
    libLicenseName = rootProject.ext.libLicenseName
    libLicenseUrl = rootProject.ext.libLicenseUrl
}

version = libVersion
group = libGroup
archivesBaseName = libArtifactId

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    // compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    api "org.jetbrains.kotlinx:kotlinx-metadata-jvm:$kotlinxMetadataVersion"
    api "com.squareup:kotlinpoet:$kotlinPoetVersion"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

install.repositories.mavenInstaller.pom.project {
    name libName
    description libDescription
    url githubUrl
    groupId libGroup
    artifactId libArtifactId
    version libVersion
    packaging 'jar'
    scm {
        connection githubGit
        url githubUrl
    }
    licenses {
        license {
            name libLicenseName
            url libLicenseUrl
        }
    }
    developers {
        developer {
            id 'natario'
            name 'Mattia Iavarone'
        }
    }
}

def bintrayUser
def bintrayKey
if (System.getenv('TRAVIS') == 'true') {
    bintrayUser = System.getenv("BINTRAY_USER")
    bintrayKey = System.getenv("BINTRAY_KEY")
} else {
    Properties props = new Properties()
    props.load(project.rootProject.file('local.properties').newDataInputStream())
    bintrayUser = props.getProperty('bintray.user')
    bintrayKey = props.get('bintray.key')
}

bintray {
    // https://github.com/bintray/gradle-bintray-plugin
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']
    override = true
    publish = true
    pkg {
        repo = 'android'
        name = libName
        licenses = [libLicenseName]
        vcsUrl = githubGit
        desc = libDescription
        version {
            name = libVersion
            desc = libName + ' v' + libVersion
            released = new Date()
            vcsTag = 'v' + libVersion
        }
    }
}